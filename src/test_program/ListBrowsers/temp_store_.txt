@echo off
setlocal enableExtensions
echo.
echo The browsers installed on this machine are:
echo.

rem for 64 bit systems
START /W REGEDIT /E "%Temp%\BROW3.reg" HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Clients\StartMenuInternet
set "bits=64"

rem for 32 bit systems
if not exist "%Temp%\BROW3.reg" (
  START /W REGEDIT /E "%Temp%\BROW3.reg" HKEY_LOCAL_MACHINE\SOFTWARE\Clients\StartMenuInternet
  set "bits=32"
)

if "%bits%" equ "64" (
 for /f "tokens=6 delims=\" %%B in ('type "%Temp%\BROW3.reg" ^| findstr /E "DefaultIcon]"') do (
   echo %%B
 )
)

if "%bits%" equ "32" (
  for /f "tokens=5 delims=\" %%B in ('type "%Temp%\BROW3.reg" ^| findstr /E "DefaultIcon]"') do (
   echo %%B
 )
)
del /Q /F "%Temp%\BROW3.reg"

rem exit /b 0

echo.
echo.
echo DEFAULT BROWSER
echo.
echo.

if "%bits%" equ "64" (
 START /W REGEDIT /E "%Temp%\BROW4.reg" HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Classes\HTTP\shell\open\command
)
if "%bits%" equ "32" (
 START /W REGEDIT /E "%Temp%\BROW4.reg" HKEY_LOCAL_MACHINE\SOFTWARE\Classes\HTTP\shell\open\command
)


setlocal enableDelayedExpansion
for /f tokens^=3^ delims^=^" %%B in ('type "%Temp%\BROW4.reg" ^| find "@"') do (
	set "default=%%B"
	set "default=!default:\\=\!"
	set "default=!default:~0,-1!"
	for %%D in ("!default!") do echo %%~nD
)
endlocal
del /Q /F "%Temp%\BROW4.reg"
pause